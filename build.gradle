plugins {
	id "com.jfrog.bintray" version "1.6"
}

group 'io.cogswell'
version '1.1.0'
Properties props = new Properties()
props.load(new File('settings.gradle').newDataInputStream())
def packageName =  props.getProperty('rootProject.name').replaceAll("'","")

allprojects {
	repositories {
		jcenter()
	}
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
	apply plugin: 'java'
}

tasks.withType(JavaCompile) {
	options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

sourceCompatibility = 1.8

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        dryRun = false
        publish = System.getenv('BINTRAY_PUBLISH')=='true' 
	publications = ['MyPublication']
	pkg {
		repo = 'maven' // always maven
		name = packageName
		desc = 'cogswell.io: Cogs Java Client SDK'
		websiteUrl = 'https://cogswell.io/#'
		userOrg = user // bintray username
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/cogswell-io/cogs-java-client-sdk'
		labels = ['iot','message broker','big data']
		publicDownloadNumbers = true
		version {
			released = new Date()
		}
	}
        println ''+(dryRun?'DRY RUN':'')+' Publishing '+pkg.userOrg+'/'+pkg.repo+'/'+pkg.name+'/'+version+' NOTE: '+(publish?'This package will be automatically published':'Someone will need to manually publish this package at bintray.com')
}

dependencies {
    compile 'org.json:json:20150729'
    compile 'javax.websocket:javax.websocket-client-api:1.1'
    compile 'org.glassfish.tyrus:tyrus-client:1.3.3'
    compile 'org.glassfish.tyrus:tyrus-container-grizzly-client:1.3.3'
	testCompile 'junit:junit:4.11'
}

jar {
	from {
		["LICENSE.txt","NOTICE.txt"]
	}
}

configurations {
	itestCompile.extendsFrom testCompile
	itestRuntime.extendsFrom testRuntime
}

sourceSets {
	itest {
		compileClasspath += main.output// + test.output
		runtimeClasspath += main.output// + test.output

		java.srcDir file('src/integration-test/java');
	}
}

test {
    testLogging {
        events "passed", "skipped", "failed"//, "standardOut", "standardError"
    }
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.12'
}

task itest(type: Test) {
	testClassesDir = sourceSets.itest.output.classesDir
	classpath = sourceSets.itest.runtimeClasspath

	testLogging {
		events "passed", "skipped", "failed", "standardOut", "standardError"
	}
}